
<!DOCTYPE html>
<html lang="en">
<head>
	<title>three.js - gpu particle system</title>
	<meta charset="utf-8">
	<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
	<style>
	body {
		font-family: Monospace;
		background-color: #f0f0f0;
		margin: 0px;
		overflow: hidden;
	}

	#info {
		position: absolute;
		top: 0px;
		width: 100%;
		padding: 5px;
		font-family:Monospace;
		font-size:13px;
		text-align:center;
		color: #ffffff;
	}

	a {
		color: #ffffff;
	}
	</style>
</head>

<body>

	<div id="container"></div>
	<!--
	<div id="info">
		<a href="https://threejs.org" target="_blank" rel="noopener">three.js</a> - GPU particle system plugin by <a href="http://charliehoey.com">Charlie Hoey</a>.
	</div>
	-->

	<script src="./js/three.js"></script>
	<script src="./js/dat.gui.min.js"></script>
	<script src="./js/TrackballControls.js"></script>
	<script src="./js/GPUParticleSystem.js"></script>
	<script id="vertexShader" type="x-shader/x-vertex">
	
		//
		// GLSL textureless classic 3D noise "cnoise",
		// with an RSL-style periodic variant "pnoise".
		// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
		// Version: 2011-10-11
		//
		// Many thanks to Ian McEwan of Ashima Arts for the
		// ideas for permutation and gradient selection.
		//
		// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
		// Distributed under the MIT license. See LICENSE file.
		// https://github.com/stegu/webgl-noise
		//

		vec3 mod289(vec3 x)
		{
		  return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec4 mod289(vec4 x)
		{
		  return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec4 permute(vec4 x)
		{
		  return mod289(((x*34.0)+1.0)*x);
		}

		vec4 taylorInvSqrt(vec4 r)
		{
		  return 1.79284291400159 - 0.85373472095314 * r;
		}

		vec3 fade(vec3 t) {
		  return t*t*t*(t*(t*6.0-15.0)+10.0);
		}

		// Classic Perlin noise
		float cnoise(vec3 P)
		{
		  vec3 Pi0 = floor(P); // Integer part for indexing
		  vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
		  Pi0 = mod289(Pi0);
		  Pi1 = mod289(Pi1);
		  vec3 Pf0 = fract(P); // Fractional part for interpolation
		  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
		  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
		  vec4 iy = vec4(Pi0.yy, Pi1.yy);
		  vec4 iz0 = Pi0.zzzz;
		  vec4 iz1 = Pi1.zzzz;

		  vec4 ixy = permute(permute(ix) + iy);
		  vec4 ixy0 = permute(ixy + iz0);
		  vec4 ixy1 = permute(ixy + iz1);

		  vec4 gx0 = ixy0 * (1.0 / 7.0);
		  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
		  gx0 = fract(gx0);
		  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
		  vec4 sz0 = step(gz0, vec4(0.0));
		  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
		  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

		  vec4 gx1 = ixy1 * (1.0 / 7.0);
		  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
		  gx1 = fract(gx1);
		  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
		  vec4 sz1 = step(gz1, vec4(0.0));
		  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
		  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

		  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
		  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
		  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
		  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
		  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
		  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
		  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
		  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

		  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
		  g000 *= norm0.x;
		  g010 *= norm0.y;
		  g100 *= norm0.z;
		  g110 *= norm0.w;
		  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
		  g001 *= norm1.x;
		  g011 *= norm1.y;
		  g101 *= norm1.z;
		  g111 *= norm1.w;

		  float n000 = dot(g000, Pf0);
		  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
		  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
		  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
		  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
		  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
		  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
		  float n111 = dot(g111, Pf1);

		  vec3 fade_xyz = fade(Pf0);
		  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
		  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
		  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
		  return 2.2 * n_xyz;
		}

		// Classic Perlin noise, periodic variant
		float pnoise(vec3 P, vec3 rep)
		{
		  vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
		  vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
		  Pi0 = mod289(Pi0);
		  Pi1 = mod289(Pi1);
		  vec3 Pf0 = fract(P); // Fractional part for interpolation
		  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
		  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
		  vec4 iy = vec4(Pi0.yy, Pi1.yy);
		  vec4 iz0 = Pi0.zzzz;
		  vec4 iz1 = Pi1.zzzz;

		  vec4 ixy = permute(permute(ix) + iy);
		  vec4 ixy0 = permute(ixy + iz0);
		  vec4 ixy1 = permute(ixy + iz1);

		  vec4 gx0 = ixy0 * (1.0 / 7.0);
		  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
		  gx0 = fract(gx0);
		  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
		  vec4 sz0 = step(gz0, vec4(0.0));
		  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
		  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

		  vec4 gx1 = ixy1 * (1.0 / 7.0);
		  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
		  gx1 = fract(gx1);
		  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
		  vec4 sz1 = step(gz1, vec4(0.0));
		  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
		  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

		  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
		  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
		  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
		  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
		  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
		  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
		  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
		  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

		  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
		  g000 *= norm0.x;
		  g010 *= norm0.y;
		  g100 *= norm0.z;
		  g110 *= norm0.w;
		  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
		  g001 *= norm1.x;
		  g011 *= norm1.y;
		  g101 *= norm1.z;
		  g111 *= norm1.w;

		  float n000 = dot(g000, Pf0);
		  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
		  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
		  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
		  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
		  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
		  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
		  float n111 = dot(g111, Pf1);

		  vec3 fade_xyz = fade(Pf0);
		  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
		  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
		  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
		  return 2.2 * n_xyz;
		}
		
		uniform float uTime;
		uniform float uScale;
		uniform sampler2D tNoise;
		uniform float windDirection;
		uniform float windPower;
		
		attribute vec3 positionStart;
		attribute float startTime;
		attribute vec3 velocity;
		attribute float turbulence;
		attribute vec3 color;
		attribute float size;
		attribute float lifeTime;
		attribute float spriteNum;
		
		varying vec4 vColor;
		varying float lifeLeft;
		varying float vWhichSprite;
		
		void main() {
			vColor = vec4( color, 1.0 );
			vec3 newPosition;
			vec3 v;
			vWhichSprite = spriteNum;
			
			float timeElapsed = uTime - startTime;
			lifeLeft = 1.0 - ( timeElapsed / lifeTime );
			gl_PointSize = ( uScale * size ) * lifeLeft;
			//float noisePow = cnoise(position);

			v.x = ( velocity.x - 0.5 ) * 3.0 + windPower*windDirection;
			v.y = ( velocity.y - 0.5 ) * 3.0 -  (9.8 * timeElapsed * 0.1) - (windPower);
			v.z = ( velocity.z - 0.5 ) * 3.0;
			newPosition = positionStart + ( v * 10.0 ) * timeElapsed;
			vec3 noise = texture2D( tNoise, vec2( newPosition.x * 0.015 + ( uTime * 0.05 ), newPosition.y * 0.02 + ( uTime * 0.015 ) ) ).rgb;
			vec3 noiseVel = ( noise.rgb - 0.5 ) * 30.0;
			vec3 displace = vec3(noiseVel * (turbulence));
			newPosition = mix( newPosition, newPosition + displace, ( timeElapsed / lifeTime ) );
			if( v.y > 0. && v.y < .05 ) {
				lifeLeft = 0.0;
			}
			if( v.x < - 1.45 ) {
				lifeLeft = 0.0;
			}
			if( timeElapsed > 0.0 ) {
				gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );
			} else {
				gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
				lifeLeft = 0.0;
				gl_PointSize = 0.;
			}
		}

		</script>
		<script id = "fragmentShader" type="x-shader/x-vertex">
		float scaleLinear( float value, vec2 valueDomain ) {
			return ( value - valueDomain.x ) / ( valueDomain.y - valueDomain.x );
		}
		
		float scaleLinear( float value, vec2 valueDomain, vec2 valueRange ) {
			return mix( valueRange.x, valueRange.y, scaleLinear( value, valueDomain ) );
		}
		
		varying vec4 vColor;
		varying float lifeLeft;
		varying float vWhichSprite;
		
		uniform sampler2D tSprite;
		uniform sampler2D tSprite0;
		uniform sampler2D tSprite1;
		uniform sampler2D tSprite2;
		uniform sampler2D tSprite3;
		uniform sampler2D tSprite4;

		void main() {
			float alpha = 0.;
			if( lifeLeft > 0.995 ) {
				alpha = scaleLinear( lifeLeft, vec2( 1.0, 0.995 ), vec2( 0.0, 1.0 ) );
			} else {
				alpha = lifeLeft * 0.75;
			}
			//vec4 tex = texture2D( tSprite3, gl_PointCoord );
			vec4 tex = texture2D(tSprite0, gl_PointCoord);
				if (vWhichSprite == 0.0) {
					tex = texture2D( tSprite0, gl_PointCoord );
				} else if (vWhichSprite == 1.0) {
					tex = texture2D( tSprite1, gl_PointCoord );
				} else if (vWhichSprite == 2.0) {
					tex = texture2D( tSprite2, gl_PointCoord );
				} else if (vWhichSprite == 3.0) {
					tex = texture2D( tSprite3, gl_PointCoord );
				} else  {
					tex = texture2D( tSprite4, gl_PointCoord );
				}

			gl_FragColor = vec4( vColor.rgb * tex.a, alpha * tex.a );
		}
	</script>

	<script>
		var camera, tick = 0,
			scene, renderer, clock = new THREE.Clock(),
			controls, container, gui = new dat.GUI( { width: 350 } ),
			options, spawnerOptions, particleSystem;

		var vs = document.getElementById( 'vertexShader' ).textContent;
		var fs = document.getElementById( 'fragmentShader' ).textContent;
		
		init();
		animate();

		function init() {

			//

			container = document.getElementById( 'container' );

			camera = new THREE.PerspectiveCamera( 28, window.innerWidth / window.innerHeight, 1, 10000 );
			camera.position.z = 20;

			scene = new THREE.Scene();

			// The GPU Particle system extends THREE.Object3D, and so you can use it
			// as you would any other scene graph component.	Particle positions will be
			// relative to the position of the particle system, but you will probably only need one
			// system for your whole scene
			
			// Load the shaders from here since it's easier to read
			particleSystem = new THREE.GPUParticleSystem( {
				uniforms:{windDirection: {value:0.0}, windPower: {value:1.0}},
				shaders:{vertexShader:vs,fragmentShader:fs},
				maxParticles: 250000
			} );

			scene.add( particleSystem );

			// options passed during each spawned

			options = {
				position: new THREE.Vector3(0,7,0),
				positionRandomness: 50.0,
				velocity: new THREE.Vector3(0,0,0),
				velocityRandomness: .5,
				color: "#aebdcf",
				colorRandomness: .2,
				windDirection:0.0,
				windPower:1.0,
				turbulence: 0.0,
				lifetime: 2,
				size: 20,
				sizeRandomness: 1
			};

			spawnerOptions = {
				spawnRate: 5000,
				horizontalSpeed: 1.5,
				verticalSpeed: 1.33,
				timeScale: 1
			};

			//
			
			var updateColor = function (){
				var colorObj = new THREE.Color(options.color);
				};
			
			gui.add( options, "velocityRandomness", 0, 3 );
			gui.add( options, "positionRandomness", 0, 100 );
			gui.add( options, "size", 10, 30 );
			gui.add( options, "sizeRandomness", 0, 25 );
			gui.add( options, "colorRandomness", 0, 1 );
			gui.add( options, "lifetime", .1, 10 );
			gui.add( options, "turbulence", 0, 1 );
			gui.add( options, "windPower", 0,1).onChange(function(value){
				particleSystem.particleShaderMat.uniforms.windPower.value = value;
			});
			gui.add( options, "windDirection", -1,1).onChange(function(value){
				particleSystem.particleShaderMat.uniforms.windDirection.value = value;
			});
			gui.addColor( options, "color").onChange(updateColor);

			gui.add( spawnerOptions, "spawnRate", 10, 30000 );
			gui.add( spawnerOptions, "timeScale", -1, 5 );

			//

			//

			renderer = new THREE.WebGLRenderer();
			renderer.setPixelRatio( window.devicePixelRatio );
			renderer.setSize( window.innerWidth, window.innerHeight );
			container.appendChild( renderer.domElement );

			//

			controls = new THREE.TrackballControls( camera, renderer.domElement );
			controls.rotateSpeed = 5.0;
			controls.zoomSpeed = 2.2;
			controls.panSpeed = 1;
			controls.dynamicDampingFactor = 0.3;

			window.addEventListener( 'resize', onWindowResize, false );

		}

		function onWindowResize() {

			camera.aspect = window.innerWidth / window.innerHeight;
			camera.updateProjectionMatrix();

			renderer.setSize( window.innerWidth, window.innerHeight );

		}

		function animate() {

			requestAnimationFrame( animate );

			controls.update();

			var delta = clock.getDelta() * spawnerOptions.timeScale/10;

			tick += delta;

			if ( tick < 0 ) tick = 0;

			if ( delta > 0 ) {

				//options.position.x = Math.sin( tick * spawnerOptions.horizontalSpeed ) * 20;
				//options.position.y = Math.sin( tick * spawnerOptions.verticalSpeed ) * 10;
				//options.position.z = Math.sin( tick * spawnerOptions.horizontalSpeed + spawnerOptions.verticalSpeed ) * 5;

				for ( var x = 0; x < spawnerOptions.spawnRate * delta; x++ ) {

					// Yep, that's really it.	Spawning particles is super cheap, and once you spawn them, the rest of
					// their lifecycle is handled entirely on the GPU, driven by a time uniform updated below

					particleSystem.spawnParticle( options );

				}

			}

			particleSystem.update( tick );

			render();

		}

		function render() {

			renderer.render( scene, camera );

		}

	</script>
</body>

</html>

